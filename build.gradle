plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "edu.wpi.first.GradleRIO" version "2024.3.1"
    id 'edu.wpi.first.WpilibTools' version '1.3.0'
}

def javaVersion = JavaVersion.VERSION_17
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
version = '2024.2.4'
group = 'org.a05annex'

wpilibTools.deps.wpilibVersion = wpi.versions.wpilibVersion.get()

def nativeConfigName = 'wpilibNatives'
def nativeConfig = configurations.create(nativeConfigName)

def nativeTasks = wpilibTools.createExtractionTasks {
    configurationName = nativeConfigName
}

nativeTasks.addToSourceSetResources(sourceSets.main)
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpimath")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpinet")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("wpiutil")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("ntcore")
nativeConfig.dependencies.add wpilibTools.deps.wpilib("cscore")
nativeConfig.dependencies.add wpilibTools.deps.wpilibOpenCv("frc" + wpi.frcYear.get(), wpi.versions.opencvVersion.get())

// Set to true to use debug for JNI.
wpi.java.debugJni = false

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.a05annex:a05annexUtil:0.9.6'
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'org.jetbrains:annotations:23.0.0'
    api 'com.googlecode.json-simple:json-simple:1.1.1'

    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation wpilibTools.deps.wpilibJava("wpiutil")
    testImplementation wpilibTools.deps.wpilibJava("wpimath")
    testImplementation wpilibTools.deps.wpilibJava("wpinet")
    testImplementation wpilibTools.deps.wpilibJava("ntcore")
    testImplementation wpilibTools.deps.wpilibJava("cscore")
    testImplementation wpilibTools.deps.wpilibJava("cameraserver")
    testImplementation wpilibTools.deps.wpilibOpenCvJava("frc" + wpi.frcYear.get(), wpi.versions.opencvVersion.get())

    testImplementation group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: wpi.versions.jacksonVersion.get()
    testImplementation group: "com.fasterxml.jackson.core", name: "jackson-core", version: wpi.versions.jacksonVersion.get()
    testImplementation group: "com.fasterxml.jackson.core", name: "jackson-databind", version: wpi.versions.jacksonVersion.get()

    testImplementation group: "org.ejml", name: "ejml-simple", version: wpi.versions.ejmlVersion.get()
    testImplementation group: "us.hebi.quickbuf", name: "quickbuf-runtime", version: wpi.versions.quickbufVersion.get();
    testImplementation('junit:junit:4.13.2')
    testImplementation('net.sourceforge.argparse4j:argparse4j:0.8.1')
    testImplementation('org.junit.platform:junit-platform-runner:1.9.0')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.0')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.9.0')
    testImplementation('org.mockito:mockito-core:4.8.0')
    testRuntimeOnly('org.a05annex:a05annexUtil:0.9.6')
}


java {
    withJavadocJar()
    withSourcesJar()
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

signing {
    if (project.hasProperty("signing.keyId") &&
            project.hasProperty("signing.password") &&
            project.hasProperty("signing.secretKeyRingFile") )
    {
        sign publishing.publications
    }
}

publishing {
    repositories {
        maven {
            name = "Sonatype"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'a05annexRobot'
            from components.java
            pom {
                name = 'A05annex Robot Library'
                description = 'A05annex robot base for swerve and NavX.'
                url = 'https://github.com/A05annex/a05annexRobot'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'A05annex'
                        name = 'A05annex'
                        email = 'a05annexrobotics@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/A05annex/a05annexRobot.git'
                    developerConnection = 'scm:git:ssh://github.com/A05annex/a05annexRobot.git'
                    url = 'https://github.com/A05annex/a05annexRobot'
                }
            }
        }
    }
}


wpi.java.configureTestTasks(test)

wrapper {
    gradleVersion = '8.5'
}




